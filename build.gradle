/*
* Copyright (c) 2011 the original author or authors
*
* Licensed under the Apache License, Version 2.0 (the "License");
* you may not use this file except in compliance with the License.
* You may obtain a copy of the License at
*
*      http://www.apache.org/licenses/LICENSE-2.0
*
* Unless required by applicable law or agreed to in writing, software
* distributed under the License is distributed on an "AS IS" BASIS,
* WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
* See the License for the specific language governing permissions and
* limitations under the License.
*/

plugins {
    id 'groovy'
    id 'maven-publish'
    id 'idea'
    id 'java-gradle-plugin'
}

group = 'org.gradlefx'
version = '1.5.1'
ext.packaging = 'jar'

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.codehaus.groovy:groovy-all:3.0.7'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'javadoc'
    from 'build/docs/groovydoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives groovydocJar
    archives sourcesJar
}

gradlePlugin {
    automatedPublishing = true
    plugins {
        gradleFxPlugin {
            id = 'org.gradlefx.gradlefx'
            implementationClass = 'org.gradlefx.plugins.GradleFxPlugin'
        }
        idePlugin {
            id = 'org.gradlefx.ide'
            implementationClass = 'org.gradlefx.ide.plugins.IDEPlugin'
        }
        fdtPlugin {
            id = 'org.gradlefx.fdt'
            implementationClass = 'org.gradlefx.ide.plugins.FDTPlugin'
        }
        flashBuilderPlugin {
            id = 'org.gradlefx.flashbuilder'
            implementationClass = 'org.gradlefx.ide.plugins.FlashBuilderPlugin'
        }
        flashDevelopPlugin {
            id = 'org.gradlefx.flashdevelop'
            implementationClass = 'org.gradlefx.ide.plugins.FlashDevelopPlugin'
        }
        ideaFxPlugin {
            id = 'org.gradlefx.ideaplugin'
            implementationClass = 'org.gradlefx.ide.plugins.IdeaPlugin'
        }
        templatesPlugin {
            id = 'org.gradlefx.templates'
            implementationClass = 'org.gradlefx.templates.plugins.TemplatesPlugin'
        }
        scaffoldPlugin {
            id = 'org.gradlefx.scaffold'
            implementationClass = 'org.gradlefx.templates.plugins.ScaffoldPlugin'
        }
        creatorPlugin {
            id = 'org.gradlefx.creator'
            implementationClass = 'org.gradlefx.templates.plugins.CreatorPlugin'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = "gradlefx"
            from components.java
            artifact groovydocJar
            artifact sourcesJar
            pom {
                name = 'GradleFx'
                packaging = 'jar'
                description 'GradleFx is a Gradle plugin for building Flex and Actionscript applications'
                url = 'http://gradlefx.github.com/'

                scm {
                    url = 'scm:git@github.com:GradleFx/GradleFx.git'
                    connection = 'scm:git@github.com:GradleFx/GradleFx.git'
                    developerConnection = 'scm:git@github.com:GradleFx/GradleFx.git'
                }

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = 'yennicktrevels'
                        name = 'Yennick Trevels'
                    }
                    developer {
                        id = 'stevendick'
                        name = 'Steven Dick'
                    }
                }
            }
        }
    }
}
